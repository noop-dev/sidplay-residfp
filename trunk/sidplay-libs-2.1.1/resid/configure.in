dnl Process this file with autoconf to produce a configure script.
AC_INIT([resid], [0.16])
AC_CONFIG_SRCDIR([sid.h])

dnl Use Automake
AM_INIT_AUTOMAKE

LTVERSION=5:0:0

dnl Use C++ for tests.
AC_LANG_CPLUSPLUS

dnl Enable inlining.
AC_ARG_ENABLE(inline,
[AS_HELP_STRING([--enable-inline],[enable inlining of functions [default=yes]])])

if test "$enable_inline" != no; then
  RESID_INLINE=inline
else
  RESID_INLINE=
fi

AC_SUBST([RESID_INLINE])

dnl Checks for programs.
AC_PROG_CXX

dnl Set CXXFLAGS for g++. Use -msse if supported.
if test "$GXX" = yes; then
  if test "$ac_test_CXXFLAGS" != set; then
    CXXFLAGS="-g -Wall -O2 -msse -mfpmath=sse"
    AC_MSG_CHECKING([whether the C++ compiler ($CXX $CXXFLAGS $LDFLAGS) works])
    AC_TRY_COMPILE([],
                   [int test;],
                   [ AC_MSG_RESULT(yes)
                     MSSE="-msse -mfpmath=sse"
                     ],
                   [ AC_MSG_RESULT(no)
                     MSSE=""
                     ])
  fi
fi

dnl Set CXXFLAGS for g++. Use -fno-exceptions if supported.
if test "$GXX" = yes; then
  if test "$ac_test_CXXFLAGS" != set; then
    CXXFLAGS="-fno-exceptions"
    AC_MSG_CHECKING([whether the C++ compiler ($CXX $CXXFLAGS $LDFLAGS) works])
    AC_TRY_COMPILE([],
                   [int test;],
                   [ AC_MSG_RESULT(yes)
                     NO_EXCEPTIONS="-fno-exceptions" ],
                   [ AC_MSG_RESULT(no)
                     NO_EXCEPTIONS=""
                     ])
  fi
fi

CXXFLAGS="-g -Wall -O3 -ffast-math $MSSE $NO_EXCEPTIONS"

AC_CHECK_PROG(AR, ar, ar, ar)
AC_PATH_PROG(PERL, perl)

dnl Checks for header files.

dnl Checks for typedefs, structures, and compiler characteristics.
AC_CHECK_SIZEOF(int, 4)

if test $ac_cv_sizeof_int -lt 4; then
  AC_MSG_ERROR([only 32 bit or better CPUs are supported])
fi

AC_CACHE_CHECK([for working bool], ac_cv_cxx_bool,
[AC_TRY_COMPILE(,
[
bool flag;
],
ac_cv_cxx_bool=yes, ac_cv_cxx_bool=no)])

if test $ac_cv_cxx_bool = no; then
  RESID_HAVE_BOOL=0
else
  RESID_HAVE_BOOL=1
fi

AC_SUBST(RESID_HAVE_BOOL)

dnl Libtool
dnl AC_DISABLE_SHARED
AC_PROG_LIBTOOL
AC_SUBST([LTVERSION])

AC_ARG_ENABLE([resid-install],
[AS_HELP_STRING([--enable-resid-install],[Enable installable library (default: disabled)])],
[my_resid_install="$enableval"], [my_resid_install="no"])

AM_CONDITIONAL([INSTALL_RESID], [test "x$my_resid_install" = "xyes"])

dnl Checks for library functions.
AC_CONFIG_FILES([
Makefile
siddefs.h
])

AC_OUTPUT
